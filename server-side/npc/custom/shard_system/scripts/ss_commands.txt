-	script	SSCommands	-1,{
end;

	OnInit:
		function getShardKey;
		bindatcmd("shardinfo", "SSCommands::OnInfo",0,100,1);
		bindatcmd("remakeshard", "SSCommands::OnRemake",70,100,1);
		bindatcmd("newshard", "SSCommands::OnNewShard",70,100,1);
		bindatcmd("listshards","SSCommands::OnListShards",60,100,1);
    end;
	
	function getShardKey {
        return (getequipcardid(getarg(0), 2) << 16) + getequipcardid(getarg(0), 3);
    }

    // @shardinfo
    // Provide the invoking player with equipped shard information.
    // ItemID, ItemName, SBaseExp, SBaseNextLvl, DynamicBonus
	// Add into config time that people will hsve to wait before getting info again
	// Option with variable $ShardInfoWait
	// In the mean time it'll return previous acquired info;
	// Done to lessen the impact on SQL
	// Making it 2 minutes on default.
	OnInfo:
		if( !$ShardInfoWait )
			$ShardInfoWait = 2*60; //2 minutes on default

		if( !EQI_ACC_L_FLAG && !EQI_ACC_R_FLAG ) {
			dispbottom "You don't have any Shard equipped.";
			end;
		}
		
		if( gettimetick(2) > @lastshardinfo + $ShardInfoWait ) {
			//Update info
			@LeftFlag = EQI_ACC_L_FLAG;
			if( EQI_ACC_L_FLAG ) {
				.@key = getShardKey(EQI_ACC_L);
				query_sql("SELECT (`item_id`,`item_name`,`base_exp`,`level`) FROM `shard_db` WHERE `id` = " + .@key,@L_shardID,@L_shardName$,@L_shardExp,@L_shardLevel);
			}
			
			@RightFlag = EQI_ACC_R_FLAG;
			if( EQI_ACC_R_FLAG ) {
				.@key = getShardKey(EQI_ACC_R);
				query_sql("SELECT (`item_id`,`item_name`,`base_exp`,`level`) FROM `shard_db` WHERE `id` = " + .@key,@R_shardID,@R_shardName$,@R_shardExp,@R_shardLevel);
			}
			@lastshardinfo = gettimetick(2);
		} else {
			dispbottom "Old information is presented. Please wait " + callfunc("Time2Str",@lastshardinfo + $ShardInfoWait) + " to get refreshed information.";
		}
		
		if( @LeftFlag ) {
			dispbottom "Left Accessory Slot Shard Information.";
			dispbottom "Item ID: " + @L_shardID;
			dispbottom "Name: " + @L_shardName$;
			dispbottom "Experience Points: " + @L_shardExp;
			dispbottom "Experience for next level: " + (@L_shardLevel * $shard_base_exp * $server_rate);
			// dispbottom "Applied Bonus: " + ???;
		}
		if( @RightFlag ) {
			dispbottom "Right Accessory Slot Shard Information.";
			dispbottom "Item ID: " + @R_shardID;
			dispbottom "Name: " + @R_shardName$;
			dispbottom "Experience Points: " + @R_shardExp;
			dispbottom "Experience for next level: " + (@R_shardLevel * $shard_base_exp * $server_rate);
			// dispbottom "Applied Bonus: " + ???;
		}
    end;

    // @remakeshard <key>
    // GM shard retrieval tool. Used to recreate shards for
    // any player who may have lost it.
	OnRemake:
		if( .@atcmd_numparameters != 1 ) {
			dispbottom "Usage: @remakeshard <numerical key>";
			end;
		}
	
		.@key = atoi(.@atcmd_parameters$[0]);
		if( !.@key ) {
			dispbottom "Invalid key provided.";
			end;
		}
	
		if( query_sql("SELECT (`item_id`,`owner_cid`,`wielder_cid`) FROM `shard_db` WHERE `id` = " + .@key,.@shard_id,.@ocid,.@wcid) < 1 ) {
			dispbottom "There's no shard with such key.";
			end;
		}
	
		.@card2 = .@key >> 16;
		.@card3 = .@key & 65535;
		
		getitem2 .@shard_id,1,1,0,0,-255,.@card2,.@card3,0;
		
		dispbottom "Shard recreated. Last stored information:";
		dispbottom "Wielder CharID: " + .@wcid + " Creator CharID: " + .@ocid;
		dispbottom "Please recheck the information and make sure that previous copy is indeed lost before giving the item to the player.";
	end;

	// @newshard <Item ID>,{<charID>}
    // GM new shard creation. Created shard <Item ID>
	// either on rewoking player, or character whose ID is provided.
	OnNewShard:
		if( .@atcmd_numparameters < 1 || .@atcmd_numparameters > 2 ) {
			dispbottom "Usage: @newshard <Item ID>,{<charID/Name>}";
			dispbottom "Rewoking player will get the shard if no CharID provided.";
			end;
		}
	
		.@shard_id = atoi(.@atcmd_parameters$[0]);
		if( !.@shard_id || getitemname(.@shard_id) == "null" ) {
			dispbottom "There's no such item ID.";
			end;
		}
	
		.@flag = .@atcmd_parameters$[1]?1:0;	
	
		switch(.@flag) {
			case 1: //CharID provided
				.@cid = atoi(.@atcmd_parameters$[1]);
				if( !.@cid ) {
					dispbottom "Invalid CharID provided.";
					end;
				}
				
				if( query_sql("SELECT (`account_id`,`name`) FROM `char` WHERE `char_id` = " + .@cid, .@aid, .@name$) < 1 ) {
					dispbottom "There is no character with such charID/Name.";
					end;
				}
				
				.@gmaid = playerattached();
				if( !attachrid(.@aid) ) {
					if( attachrid(.@gmaid) )
						dispbottom "Destined character is offline.";
					end;
				}
				
				if( getcharid(0) != .@cid ) {
					if( attachrid(.@gmaid) )
						dispbottom "Destined player is on the right account, but on the wrong character.";
					end;
				}
			break;
			case 0:
				.@cid = getcharid(0);
				.@aid = getcharid(3); 
				.@name$ = strcharinfo(PC_NAME);
			break;
			
			default:
				dispbottom "An error occured. Please retry the command.";
			break;
		}
	
		// Create a new shard entry in the DB.
	    query_sql("INSERT INTO `shard_db` (`owner_cid`, `owner_name`, `owner_aid`, `item_id`, `item_name`, `date_created`) VALUES("+ .@cid +", '"+ escape_sql(.@name$) +"', "+ .@aid +", "+ .@shard_id +", '"+ escape_sql(getitemname(.@shard_id)) +"', GETDATE()) ");
	
	    // Select the newest shard entry.
	    query_sql("SELECT `id` FROM `shard_db` WHERE `owner_id` = "+ .@cid +" ORDER BY `date_created` DESC LIMIT 1", .@shard_key);
	
	    // Segment the 32-bit primary key.
	    .@card2 = .@shard_key[0] >> 16;
	    .@card3 = .@shard_key[0] & 65535;
	
	    // Provide the player with the shard.
	    getitem2 .@shard_id,1,1,0,0,-255,.@card2,.@card3,0;
	    return;
	end;
	
	// @listshards <Name/CharID>;
	// Provides info on shards created(in the future wielded)
	// by the player under provided name/CharID
	// Limited to 5 characters/25 shards per character.
	OnListShards:
		if( .@atcmd_numparameters != 1 ) {
			dispbottom "Usage: @listshards <Name/CharID>";
			dispbottom "For better results use CharID over Name.";
			end;
		}
		
		.@desc$ = .@atcmd_parameters$[1];
		if( .@count = query_sql("SELECT (`account_id`,`char_id`,`name`) FROM `char` WHERE `char_id` = " + atoi(.@desc$) + " OR `name` LIKE '%" + escape_sql(.@desc$) + "%'",.@aid,.@cid,.@name$) < 1 ) {
			dispbottom "No characters with such name or charID found.";
			end;
		}
		
		if( .@count > 5 ) {
			dispbottom "Results for first 5 found players will be shown.";
			.@count = 5;
		}
		
		for( .@i = 0; .@i < .@count; .@i++) {
			dispbottom "Character #" + .@i + ". ";
			dispbottom "Name: " + .@name$[.@i] + " CharID: " + .@cid[.@i] + " AccID:" + .@aid[.@i];
			if( .@count2 = query_sql("SELECT (`id`,`item_id`,`item_name`,`level`,`owner_cid`,`wielder_cid`) FROM `shard_db` WHERE `owner_cid` = " + .@cid[.@i] + " OR `wielder_cid` = " + .@cid[.@i] + " ORDER BY `date_created` DESC",.@sid,.@siid,.@sin$,.@sl,.@ocid,.@wcid) < 1 ) {
				dispbottom "      Player has no registered created or wielded shards.";
				continue;
			} else {
				if( .@count2 > 25 ) {
					dispbottom "      Only last 25 found shards will be shown. For more info please refer to your Head GM/Developer.";
					.@count2 = 25;
				}
				for( .@a = 0; .@a < .@count2; .@a++ ) {
					dispbottom "      Shard Key: " + .@sid[.@a] + " Shard Level: " + .@sl[.@a] + " Item ID: " + .@siid[.@a] + " Item Name: " + .@sin$[.@a];
					dispbottom "      Wielder CharID: " + .@wcid[.@a] + " Creator CharID: " + .@ocid[.@a];
				}
			}
		}
	end;
}
